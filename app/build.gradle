apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "quake"

    productFlavors {
        //Development
        dev {
            applicationId "com.sample.knowquake.dev"
            buildConfigField 'String', 'BASEURL', '"https://earthquake.usgs.gov/"'
            resValue "string", "app_name", "KnowQuake"
            dimension "quake"
        }

        //Production !!!!!Important!!!!!!!Don't change Application id, etc...
        clientprod {
            applicationId "com.sample.knowquake"
            buildConfigField 'String', 'BASEURL', '"https://earthquake.usgs.gov/"'
            resValue "string", "app_name", "KnowQuake"
            dimension "quake"
        }
    }

    dataBinding {
        enabled = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

}

kapt {
    generateStubs = true
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    androidTestImplementation('androidx.test.espresso:espresso-core:3.1.0-alpha4', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    // UI
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation "androidx.recyclerview:recyclerview:${rootProject.ext.recyclerViewSupportLibVersion}"
    implementation "androidx.appcompat:appcompat:${rootProject.ext.appcompatSupportLibVersion}"
    implementation "androidx.cardview:cardview:${rootProject.ext.cardSupportLibVersion}"
    implementation "androidx.browser:browser:${rootProject.ext.browserVersion}"
    implementation "androidx.constraintlayout:constraintlayout:${rootProject.ext.constraintLayoutVersion}"
    implementation "androidx.multidex:multidex:${rootProject.ext.multiDexVersion}"
    implementation "com.google.android.material:material:${rootProject.ext.materialVersion}"

    // Retrofit
    implementation "com.squareup.retrofit2:retrofit:${rootProject.ext.retrofitVersion}"
    implementation "com.squareup.retrofit2:converter-scalars:${rootProject.ext.retrofitVersion}"
    implementation "com.squareup.retrofit2:converter-gson:${rootProject.ext.retrofitVersion}"
    implementation "com.squareup.retrofit2:adapter-rxjava2:${rootProject.ext.retrofitVersion}"

    // RxJava 2
    implementation "io.reactivex.rxjava2:rxandroid:${rootProject.ext.rxAndroid}"
    implementation "io.reactivex.rxjava2:rxjava:${rootProject.ext.rxJavaVersion}"

    // Find Memory Leaks - Only on Debug Build
    debugImplementation "com.squareup.leakcanary:leakcanary-android:${rootProject.ext.leakcanary}"
    releaseImplementation "com.squareup.leakcanary:leakcanary-android-no-op:${rootProject.ext.leakcanary}"
    debugImplementation "com.squareup.leakcanary:leakcanary-support-fragment:${rootProject.ext.leakcanary}"

    // Interceptors for Logging request and response
    implementation "com.squareup.okhttp3:logging-interceptor:${rootProject.ext.okhttpVersion}"

    // Dagger 2 (Dependency Injection)
    implementation "com.google.dagger:dagger:${rootProject.ext.dagger2Version}"
    kapt "com.google.dagger:dagger-compiler:${rootProject.ext.dagger2Version}"
    kapt "com.google.dagger:dagger-android-processor:${rootProject.ext.dagger2Version}"
    implementation "com.google.dagger:dagger-android:${rootProject.ext.dagger2Version}"
    implementation "com.google.dagger:dagger-android-support:${rootProject.ext.dagger2Version}"

    // ViewModel and LiveData (Android Architecture Components)
    implementation "androidx.lifecycle:lifecycle-extensions:${rootProject.ext.lifecycle_extension_version}"
    implementation "androidx.lifecycle:lifecycle-viewmodel:${rootProject.ext.lifecycle_version}"
    implementation "androidx.lifecycle:lifecycle-livedata:${rootProject.ext.lifecycle_livedata_version}"
    implementation "androidx.lifecycle:lifecycle-runtime:${rootProject.ext.lifecycle_version}"
    implementation "androidx.lifecycle:lifecycle-common-java8:${rootProject.ext.lifecycle_version}"
    implementation "androidx.lifecycle:lifecycle-reactivestreams:${rootProject.ext.lifecycle_rxstreams_version}"
    testImplementation "androidx.arch.core:core-testing:${rootProject.ext.lifecycle_version}"

    // Instrumentation tests
    androidTestImplementation "androidx.test.espresso:espresso-core:${rootProject.ext.espressoVersion}"
    androidTestImplementation "androidx.test.espresso:espresso-contrib:${rootProject.ext.espressoVersion}"
    androidTestImplementation "com.android.support.test.espresso:espresso-intents:${rootProject.ext.espressoVersion}"
    androidTestImplementation "androidx.test:runner:${rootProject.ext.runnerVersion}"
    androidTestImplementation "androidx.test:rules:${rootProject.ext.runnerVersion}"
    androidTestImplementation "com.squareup.okhttp3:mockwebserver:${rootProject.ext.okhttpVersion}"

    // Local unit tests
    testImplementation "junit:junit:${rootProject.ext.junitVersion}"
    testImplementation "org.mockito:mockito-core:${rootProject.ext.mockitoVersion}"
    testImplementation "org.hamcrest:hamcrest-library:${rootProject.ext.hamcrestVersion}"
    testImplementation "com.squareup.okhttp3:mockwebserver:${rootProject.ext.okhttpVersion}"

}
